plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'org.jetbrains.kotlin.jvm' version '1.3.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21'
	id "org.jetbrains.kotlin.plugin.allopen" version "1.3.21"
	id "org.jetbrains.kotlin.plugin.noarg" version "1.3.21"
	id "org.jetbrains.kotlin.plugin.jpa" version "1.3.21"
	id "org.liquibase.gradle" version "2.0.1"
}

noArg {
	invokeInitializers = true
	annotation("javax.persistence.Entity")
	annotation("javax.persistence.MappedSuperclass")
	annotation("javax.persistence.Embeddable")
}

allOpen {
	annotation("javax.persistence.Entity")
	annotation("javax.persistence.MappedSuperclass")
	annotation("javax.persistence.Embeddable")
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'

group = 'com.scottlogic'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
mainClassName = 'com.scottlogic.reactivegame.ReactiveGameApplicationKt'

project.ext {
	mainUrl = "jdbc:mariadb://localhost:3306/firsttest"
	mainUsername = "ehield"
	mainPassword = "password"
}

repositories {
	mavenCentral()
}

sourceSets {
    groovyUtils
	main.kotlin.srcDirs = ['src/main/java', 'src/main/kotlin']
	main.resources.srcDirs = ['src/main/resources']
}

dependencies {
	implementation files('libs/com4j-20120426-2/com4j.jar')
	runtimeOnly files('libs/com4j-20120426-2/com4j.amd64.dll')
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.hamcrest:hamcrest-library:1.3'
	testImplementation 'io.projectreactor:reactor-test'
	liquibaseRuntime 'ch.qos.logback:logback-core:1.2.3'
	liquibaseRuntime 'ch.qos.logback:logback-classic:1.2.3'
	liquibaseRuntime 'org.liquibase:liquibase-gradle-plugin:2.0.1'
	liquibaseRuntime "org.liquibase:liquibase-core:3.6.1"
	liquibaseRuntime "org.liquibase:liquibase-groovy-dsl:2.0.1"
	liquibaseRuntime "org.mariadb.jdbc:mariadb-java-client"
    groovyUtilsCompile 'org.codehaus.groovy:groovy-all:2.5.6'
	compile 'org.apache.httpcomponents:httpclient:4.5.8'
	implementation 'io.jsonwebtoken:jjwt:0.9.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/db/main.groovy'
			url project.ext.mainUrl
			username project.ext.mainUsername
			password project.ext.mainPassword
		}
	}
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}
